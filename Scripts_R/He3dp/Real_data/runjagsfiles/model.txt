model{
# LIKELIHOOD informative
for (i in 1:N) {
obsy[i] ~ dnorm(y[i], pow(erry[i], -2))
y[i] ~ dnorm(scale[re[i]]*sfactor3Hedp(obsx[i], e1, ex, gin, gout,ri,rf,ue[ik[i]]),pow(tau, -2))
res[i] <- obsy[i]-sfactor3Hedp(obsx[i], e1,ex, gin, gout,ri,rf,0)
}
# LIKELIHOOD broad 
for (i in 1:N) {
obsy2[i] ~ dnorm(y_2[i], pow(erry[i], -2))
y_2[i] ~ dnorm(scale[re[i]]*sfactor3Hedp(obsx[i], e1_2, ex_2, gin_2, gout_2,ri_2,rf_2,ue[ik[i]]),pow(tau_2, -2))
}
RSS <- sum(res^2)
# Predicted values
for (j in 1:M){
# Bare...
mux0[j] <- sfactor3Hedp(xx[j], e1,ex, gin, gout,ri,rf,0)
mux0_2[j] <- sfactor3Hedp(xx[j], e1_2,ex_2, gin_2, gout_2,ri_2,rf_2,0)
DeltaM[j] <- (mux0[j] - mux0_2[j])/mux0[j]
# No inverse Kinematics 
mux1[j] <- sfactor3Hedp(xx[j], e1, ex,gin, gout,ri,rf,ue[1])
yx1[j] ~ dnorm(mux1[j],pow(tau,-2))
# With inverse Kinematics 
mux2[j] <- sfactor3Hedp(xx[j], e1,ex, gin, gout,ri,rf,ue[2])
yx2[j] ~ dnorm(mux1[j],pow(tau,-2))
}
for (k in 1:Nre){
scale[k] ~ dlnorm(log(1.0),1/log(1+pow(syst[k],2)))
}
for (z in 1:Nik){
ue[z] ~ dnorm(0,1e3)T(0,)
}
# PRIORS 1
tau ~  dunif(0.01,5)
e1 ~  dunif(0,5)
ex <-  e1 
gin ~  dunif(0,5)
gout ~ dunif(0,5)
rf ~  dnorm(5, pow(0.1,-2))T(0,)
ri ~  dnorm(5, pow(0.1,-2))T(0,)
tau_2 ~ dunif(0.01,5)
e1_2  ~  dunif(0,5)
ex_2  <-   e1_2
gin_2 ~  dunif(0,20)
gout_2 ~ dunif(0,20)
rf_2 ~  dunif(0,50)
ri_2 ~  dunif(0,50)
}
